# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: frownlee <florez.brownlee@gmail.com>
# Contributor: philm <philipp.mildenberger <- AT -> student.uibk.ac.at>

_pkg=android-ndk
pkgname=android-ndk-19
pkgver=r19c
pkgrel=1
pkgdesc='Android C/C++ developer kit'
arch=('x86_64')
url='https://developer.android.com/ndk/'
license=('GPL' 'LGPL' 'custom')
options=('!strip' 'staticlibs')
backup=('etc/profile.d/android-ndk-19.sh')
install="android-ndk.install"
provides=('android-ndk')
conflicts=('android-ndk')
replaces=('android-ndk64')
depends=('glibc')
source=('android-ndk.sh' 'package.xml')
source_x86_64=("https://dl.google.com/android/repository/$_pkg-$pkgver-linux-x86_64.zip")
sha1sums=('SKIP' 'SKIP')
sha1sums_x86_64=('SKIP')

package() {
  install -Ddm755 "$pkgdir/opt"
  mv "$_pkg-$pkgver" "$pkgdir/opt/$pkgname"

  install -Dm755 android-ndk.sh -t "$pkgdir/etc/profile.d"
  install -Dm644 package.xml -t "$pkgdir/opt/$pkgname"
  mkdir -p "$pkgdir/opt/android-sdk/ndk"
  ln -s "../../$pkgname" "$pkgdir/opt/android-sdk/ndk/$pkgname"
}

# vim: ts=2 sw=2 et:
