# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: frownlee <florez.brownlee@gmail.com>
_pkg=android-ndk
pkgname=android-ndk-21
pkgver=r21.b
pkgrel=1
pkgdesc='Android C/C++ developer kit'
arch=('x86_64')
url='https://developer.android.com/ndk/'
license=('GPL' 'LGPL' 'custom')
options=('!strip' 'staticlibs')
backup=("etc/profile.d/$pkgname.sh")
install="$pkgname.install"
replaces=('android-ndk64')
depends=('glibc')
optdepends=(
  'ncurses5-compat-libs: for using gdb'
  'python2: various helper scripts'
  'python: various helper scripts'
)
source=("$pkgname.sh" "package.xml")
source_x86_64=("https://dl.google.com/android/repository/$_pkg-${pkgver/./}-linux-x86_64.zip")
# SHA1 sums is kept to follow upstream releases https://github.com/android-ndk/ndk/issues/673
sha1sums=('2479a8d74428eb651ad2b9772ad655d7a90af410' 'SKIP')
sha1sums_x86_64=('SKIP')
sha256sums=('2068618bc3e99149310f544be5d639ae183dc767d5a7157a07d8e024417b6017' 'SKIP')
sha256sums_x86_64=('SKIP')

package() {
  install -Ddm755 "$pkgdir/opt"
  mv "$_pkg-${pkgver/./}" "$pkgdir/opt/$pkgname"

  install -Dm644 $pkgname.sh -t "$pkgdir/etc/profile.d"
  install -Dm644 package.xml -t "$pkgdir/opt/$pkgname"
  mkdir -p "$pkgdir/opt/android-sdk/ndk"
  ln -s "../../$pkgname" "$pkgdir/opt/android-sdk/ndk/$pkgname"
}
