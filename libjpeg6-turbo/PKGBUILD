# $Id: PKGBUILD 124693 2011-05-24 03:40:20Z eric $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libjpeg6-turbo
pkgver=1.1.1
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('glibc')
makedepends=('nasm')
provides=('libjpeg6')
conflicts=('libjpeg6')
options=('!libtool')
source=(http://sourceforge.net/projects/libjpeg-turbo/files/$pkgver/libjpeg-turbo-$pkgver.tar.gz)
md5sums=('03b9c1406c7bfdc204313c2917ce6962')

build() {
  cd "$srcdir/libjpeg-turbo-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/libjpeg-turbo-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/libjpeg6-turbo/LICENSE"

  # Fix /usr/bin
  rm $pkgdir/usr/bin/jpgtest
  for fn in $pkgdir/usr/bin/*; do mv $fn ${fn}6; done
  # Fix /usr/lib
  rm $pkgdir/usr/lib/lib{turbo,}jpeg.{a,so}
  # Fix /usr/share/man
  for fn in $pkgdir/usr/share/man/man1/*; do mv $fn ${fn%.1}6.1; done
  # Fix /usr/include
  mkdir -p $pkgdir/usr/include/libjpeg6
  mv $pkgdir/usr/include/*.h $pkgdir/usr/include/libjpeg6
}
