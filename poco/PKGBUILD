# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Ido Rosen <ido@kernel.org>
# Contributor: Ross Whitfield <whitfieldre@ornl.gov>
# Contributor: Mateusz Paluszkiewcz <aifam96 at gmail dot com>
# Contributor: Christopher Reimer <vdr4arch at creimer dot net>

pkgname=poco
pkgver=1.10.1
_pkgver=${pkgver/_/}
pkgrel=1
pkgdesc="C++ class libraries for network-centric, portable applications, complete edition with debug libraries"
arch=('x86_64')
url="http://www.pocoproject.org/"
license=('custom:boost')
depends=('mariadb-libs' 'openssl' 'unixodbc')
makedepends=('cmake')
source=("https://github.com/pocoproject/poco/archive/poco-${_pkgver}-release.tar.gz")
sha256sums=('44592a488d2830c0b4f3bfe4ae41f0c46abbfad49828d938714444e858a00818')

build() {
  cd "${srcdir}/poco-poco-${_pkgver}-release"
  mkdir -p build
  cd build
  cmake \
    -DENABLE_XML=ON \
    -DENABLE_JSON=ON \
    -DENABLE_NET=ON \
    -DENABLE_NETSSL=ON \
    -DENABLE_CRYPTO=ON \
    -DENABLE_DATA=ON \
    -DENABLE_DATA_SQLITE=ON \
    -DENABLE_DATA_MYSQL=ON \
    -DENABLE_DATA_POSTGRESQL=ON \
    -DENABLE_DATA_ODBC=ON \
    -DENABLE_MONGODB=ON \
    -DENABLE_REDIS=ON \
    -DENABLE_PDF=ON \
    -DENABLE_UTIL=ON \
    -DENABLE_ZIP=ON \
    -DENABLE_SEVENZIP=ON \
    -DENABLE_APACHECONNECTOR=OFF \
    -DENABLE_CPPPARSER=ON \
    -DENABLE_ENCODINGS=ON \
    -DENABLE_ENCODINGS_COMPILER=ON \
    -DENABLE_PAGECOMPILER=ON \
    -DENABLE_PAGECOMPILER_FILE2PAGE=ON \
    -DENABLE_POCODOC=ON \
    -DENABLE_TESTS=OFF \
    -DENABLE_LONG_RUNNING_TESTS=OFF \
    -DPOCO_UNBUNDLED=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd "${srcdir}/poco-poco-${_pkgver}-release"/build
  make DESTDIR="${pkgdir}" install
  install -Dm644 '../LICENSE' "${pkgdir}/usr/share/licenses/poco/LICENSE"
}
