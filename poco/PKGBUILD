# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Ido Rosen <ido@kernel.org>
# Contributor: Ross Whitfield <whitfieldre@ornl.gov>
# Contributor: Mateusz Paluszkiewcz <aifam96 at gmail dot com>
# Contributor: Christopher Reimer <vdr4arch at creimer dot net>

pkgname=poco
pkgver=1.11.0
_pkgver=${pkgver/_/}
pkgrel=1
pkgdesc="C++ class libraries for network-centric, portable applications, complete edition with debug libraries"
arch=('x86_64')
url="http://www.pocoproject.org/"
license=('custom:boost')
depends=('mariadb-libs' 'openssl' 'unixodbc')
makedepends=('cmake' 'ninja')
source=("https://github.com/pocoproject/poco/archive/poco-${_pkgver}-release.tar.gz")
sha256sums=('SKIP')

build() {
  cd "poco-poco-${_pkgver}-release"

  cmake \
    -DENABLE_CPPPARSER=ON \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  ninja -C build
  
  cmake \
    -DENABLE_CPPPARSER=ON \
    -GNinja \
    -Bbuild_debug \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Debug
  ninja -C build_debug
}

package() {
  cd "poco-poco-${_pkgver}-release"
  DESTDIR="${pkgdir}" ninja -C build install
  DESTDIR="${pkgdir}" ninja -C build_debug install
}
