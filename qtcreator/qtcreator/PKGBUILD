pkgbase=qtcreator
pkgname=(qtcreator qtcreator-devel)
pkgver=13.0.2
_clangver=17.0.6
_commit=13.0
pkgrel=1
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=(x86_64)
url='https://www.qt.io'
license=("Apache-2.0")
depends=(qt6-tools qt6-svg qt6-quick3d qt6-webengine qt6-serialport qt6-shadertools qt6-5compat
         clang=$_clangver clazy yaml-cpp litehtml) # syntax-highlighting
makedepends=(cmake llvm python clang)
options=(docs)
optdepends=('qt6-doc: integrated Qt documentation'
            'qt6-examples: welcome page examples'
            'qt6-translations: for other languages'
            'gdb: debugger'
            'cmake: cmake project support'
            'x11-ssh-askpass: ssh support'
            'git: git support'
            'mercurial: mercurial support'
            'bzr: bazaar support'
            'valgrind: analyze support'
            'perf: performer analyzer'
            'mlocate: locator filter')
        
source=("fancymainwindow.patch"
        "projectmodels.patch"
        "navigationwidget.patch"
        "breakpoint.patch"
        "attach.patch"
        "perf.patch"
        "doxygen.patch"
        "pythonproject.patch"
        "appstackedpane.patch"
        "pylsp.patch"
        "pyindent.patch")
sha256sums=(SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP)

groups=('modified')

prepare() {
  cd $srcdir

#  git clone --branch ${_commit} --depth 1 --shallow-submodules git@github.com:qt-creator/qt-creator.git
#  cd qt-creator
#  git submodule update --init src/libs/3rdparty/googletest
#  git submodule update --init src/shared/qbs
#  git submodule update --init src/tools/perfparser
#  cd ..

  git clone --branch ${_commit} --depth 1 --shallow-submodules --recurse-submodules git@github.com:qt-creator/qt-creator.git
  rm -r qt-creator/src/libs/qlitehtml/src/3rdparty

  patch -d qt-creator -p1 < projectmodels.patch
  patch -d qt-creator -p1 < navigationwidget.patch
  patch -d qt-creator -p1 < breakpoint.patch
  patch -d qt-creator -p1 < attach.patch  
  patch -d qt-creator -p1 < perf.patch  
  patch -d qt-creator -p1 < doxygen.patch
  patch -d qt-creator -p1 < pythonproject.patch
  patch -d qt-creator -p1 < appstackedpane.patch
  patch -d qt-creator -p1 < fancymainwindow.patch
  patch -d qt-creator -p1 < pylsp.patch
  patch -d qt-creator -p1 < pyindent.patch
}

build() {
  # Clang
#  export CC=/usr/bin/clang
#  export CXX=/usr/bin/clang++
#  export CXXFLAGS="-march=znver2 -mtune=znver2 -O2 -pipe -fno-plt -fexceptions \
#                   -Wp,-D_FORTIFY_SOURCE=2 -Wformat -Werror=format-security \
#                   -fPIC -Qunused-arguments -flto=thin"
#  export CFLAGS="-march=znver2 -mtune=znver2 -O2 -pipe -fno-plt -fexceptions \
#                   -Wp,-D_FORTIFY_SOURCE=2 -Wformat -Werror=format-security \
#                   -fPIC -Qunused-arguments -flto=thin"

  
  cmake -B build -S qt-creator \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DWITH_DOCS=ON \
    -DBUILD_DEVELOPER_DOCS=ON \
    -DBUILD_QBS=OFF \
    -DQTC_CLANG_BUILDMODE_MATCH=ON \
    -DCLANGTOOLING_LINK_CLANG_DYLIB=ON
  cmake --build build
  export QT_QPA_PLATFORMTHEME=""; cmake --build build --target docs
}

package_qtcreator() {
  DESTDIR="$pkgdir" cmake --install build
# Install docs
  cp -r build/share/doc "$pkgdir"/usr/share

  install -Dm644 qt-creator/LICENSE.GPL3-EXCEPT "$pkgdir"/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}

package_qtcreator-devel() {
  pkgdesc+=' (development files)'
  depends=(qtcreator)
  optdepends=()

  DESTDIR="$pkgdir" cmake --install build --component Devel
}