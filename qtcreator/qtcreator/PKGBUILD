pkgbase=qtcreator
pkgname=(qtcreator qtcreator-devel)
pkgver=8.0.2
_clangver=14.0.6
pkgrel=1
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=(x86_64)
url='https://www.qt.io'
license=("Apache-2.0")
depends=(qt5-tools qt5-svg qt5-quick3d qt5-webengine qt5-serialport
         clang=$_clangver clazy yaml-cpp) # syntax-highlighting
makedepends=(cmake llvm python)
options=(docs)
optdepends=('qt5-doc: integrated Qt documentation'
            'qt5-examples: welcome page examples'
            'qt5-translations: for other languages'
            'gdb: debugger'
            'cmake: cmake project support'
            'x11-ssh-askpass: ssh support'
            'git: git support'
            'mercurial: mercurial support'
            'bzr: bazaar support'
            'valgrind: analyze support'
            'perf: performer analyzer'
            'mlocate: locator filter')
        
source=("QTCREATORBUG-21681.patch"
        "projectmodels.patch"
        "navigationwidget.patch"
        "tracepoint.patch"
        qtcreator-languageclient2.patch::https://code.qt.io/cgit/qt-creator/qt-creator.git/patch/?id=bd00cc8b
        qtcreator-languageclient3.patch::https://code.qt.io/cgit/qt-creator/qt-creator.git/patch/?id=8276bd26)
sha256sums=(SKIP
            SKIP
            SKIP
            SKIP
            '1861f416f2accb75f3f8074f7caa83da2aa6183af99eb9c3d8e35183db0a5e8c'
            '08ddbe2b5fae2f8900e44c7af10397277e05c65859595d5d98256f83555e7d09')

groups=('modified')

prepare() {
  cd $srcdir
  git clone --branch 8.0 --depth 1 --recurse-submodules git@github.com:qt-creator/qt-creator.git
  patch -d qt-creator -p1 < QTCREATORBUG-21681.patch
  patch -d qt-creator -p1 < projectmodels.patch
  patch -d qt-creator -p1 < navigationwidget.patch
  patch -d qt-creator -p1 < tracepoint.patch
  
  # Backport patches to mitigate clang language server crashes
  patch -d qt-creator -p1 <qtcreator-languageclient2.patch 
  patch -d qt-creator -p1 <qtcreator-languageclient3.patch 
}

build() {
  cmake -B build -S qt-creator \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DWITH_DOCS=ON \
    -DBUILD_DEVELOPER_DOCS=ON \
    -DBUILD_QBS=OFF \
    -DQTC_CLANG_BUILDMODE_MATCH=ON \
    -DCLANGTOOLING_LINK_CLANG_DYLIB=ON
  cmake --build build
  export QT_QPA_PLATFORMTHEME=""; cmake --build build --target docs
}

package_qtcreator() {
  DESTDIR="$pkgdir" cmake --install build
# Install docs
  cp -r build/share/doc "$pkgdir"/usr/share

  install -Dm644 qt-creator/LICENSE.GPL3-EXCEPT "$pkgdir"/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}

package_qtcreator-devel() {
  pkgdesc+=' (development files)'
  depends=(qtcreator)
  optdepends=()

  DESTDIR="$pkgdir" cmake --install build --component Devel
}