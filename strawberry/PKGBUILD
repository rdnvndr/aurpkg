# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=strawberry
pkgver=0.6.13
pkgrel=1
pkgdesc="A music player aimed at audio enthusiasts and music collectors"
arch=(x86_64)
url="https://www.strawbs.org/"
license=(GPL3)
depends=(chromaprint protobuf gst-plugins-base gst-plugins-good qt5-x11extras libcdio libgpod libmtp fftw)
makedepends=(cmake boost qt5-tools)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
source=("https://github.com/jonaski/strawberry/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('0c0cee6c1f75541dcc89c3dcb2db29e79f07d7b4dabc911c89853ad0b50fba29')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < $startdir/cle.patch
}
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SYSTEM_TAGLIB=ON \
    -DENABLE_AUDIOCD=ON \
    -DENABLE_GIO=OFF \
    -DENABLE_IMOBILEDEVICE=OFF \
    -DENABLE_LIBGPOD=OFF \
    -DENABLE_LIBMTP=OFF \
    -DENABLE_LIBPULSE=OFF \
    -DENABLE_MOODBAR=OFF \
    -DENABLE_PHONON=OFF \
    -DENABLE_UDISKS2=OFF \
    -DENABLE_VLC=OFF \
    -DENABLE_SUBSONIC=OFF \
    -DENABLE_TIDAL=OFF \
    -DENABLE_XINE=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
