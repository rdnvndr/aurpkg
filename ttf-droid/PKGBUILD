# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=ttf-droid-sans-mono
pkgver=20121017
pkgrel=10
pkgdesc="General-purpose fonts released by Google as part of Android"
arch=('any')
license=('Apache')
url='https://www.droidfonts.com/'
provides=('ttf-font')
source=("https://sources.archlinux.org/other/community/ttf-droid/ttf-droid-$pkgver.tar.xz"
        65-droid-sans-mono.conf)
sha256sums=('f068efec9e8201fe0d1be75b1a4751c98d14dc44267b2237f549403b2a8069e3'
            'SKIP')

package() {
  # Install fonts
  install -Dt "$pkgdir/usr/share/fonts/droid" -m644 \
    google-droid-fonts-$pkgver/*Mono*.ttf

  # Install fontconfig
  local fcdir="$pkgdir/usr/share/fontconfig"
  install -Dt "$fcdir/conf.avail" -m644 *.conf
  mkdir -p "$fcdir/conf.default"
  ln -srt "$fcdir/conf.default" "$fcdir"/conf.avail/*
}

# vim:set sw=2 et:
