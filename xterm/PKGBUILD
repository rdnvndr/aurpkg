# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xterm
pkgver=395
pkgrel=1
pkgdesc="X Terminal Emulator"
arch=('x86_64')
url="https://invisible-island.net/xterm/"
license=('custom')
depends=('libxaw' 'ncurses' 'luit' 'xbitmaps' 'libxkbfile')
source=(https://invisible-mirror.net/archives/${pkgname}/${pkgname}-${pkgver}.tgz{,.asc}
        border.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP')
#validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # "Thomas Dickey <dickey@invisible-island.net>"
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # "Thomas E. Dickey (self-signed w/o SHA1) <dickey@invisible-island.net>"

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../border.patch
  ./configure --prefix=/usr \
      --libdir=/etc \
      --mandir=/usr/share/man \
      --with-app-defaults=/usr/share/X11/app-defaults/ \
      --with-x \
      --disable-full-tgetent \
      --disable-imake \
      --enable-ansi-color \
      --enable-88-color \
      --enable-256-color \
      --enable-broken-osc \
      --enable-broken-st \
      --enable-load-vt-fonts \
      --enable-i18n \
      --enable-wide-chars \
      --enable-doublechars \
      --enable-tcap-query \
      --enable-dabbrev \
      --enable-luit \
      --enable-mini-luit \
      --enable-narrowproto \
      --enable-exec-xterm \
      --enable-sixel-graphics \
      --enable-regis-graphics \
      --with-tty-group=tty
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  chmod 0755 "${pkgdir}/usr/bin/xterm"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m755 -d "${pkgdir}"/usr/share/applications
  install -m644 "${srcdir}"/${pkgname}-${pkgver}/{xterm,uxterm}.desktop "${pkgdir}"/usr/share/applications/
}
